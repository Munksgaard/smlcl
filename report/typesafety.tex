\section{Type Safety and Phantom Types}

When executing OpenCL kernels using the simple interface provided by
PrimCL, there is no way to guarantee type safety. Kernels are
specified in OpenCLs C-derived language, and are imported as text
files with no knowledge of which arguments are required for correct
execution, buffers are read and written completely untyped, and kernel
arguments are set with complete disregard for the actual arguments and
types required. As a result, we have no way of guaranteeing that a
given OpenCL program will execute correctly. Luckily, given an
interface like PrimCL, we can utilize Standard MLs type system to
construct a system that allows us to execute kernels in a statically
checked, type-safe manner.

\subsection{An Introduction to Phantom Types}

Suppose that we have a set of functions for creating buffers of
different types, for creating kernels, and for executing them on our
OpenCL device, like the following:

\begin{lstlisting}[language=ML, caption=Signatures for a primitive
    SmlCL module,label=smlcl1,mathescape]
signature OPENCL = sig
  type T
  val Real: T
  val Int: T
  type bufP
  type kern2

  (* Creates a buffer from a type constructor and a size *)
  val mkBuf : T -> int -> bufP

  (* Given the source code as a string, create a kernel that takes
     two buffers each specificed by a type T as arguments *)
  val mkKern2 : string -> (T * T) -> kern2

  val runKern2 : kern2 -> (bufP * bufP) -> unit
end
\end{lstlisting}

In listing \ref{smlcl1}, the type \texttt{T} defines the type of
buffers (here providing the constructors \texttt{Int} and
\texttt{Real}), type \texttt{bufP} represents a buffer, and
\texttt{kern2} is a kernel that takes two buffers as arguments and
returns one buffer as the result.

We now want to make sure that we only execute kernels with the right
combination of arguments. That is, if we've specified a kernel that
takes two \texttt{Int} buffers as arguments, we wish to make sure that
\texttt{runKern2} verifies that the types of the buffers correspond
with the types of the arguments to the kernel.

One way to do this would be to add a lot of cases to the declaration
of \texttt{runKern2}, like so:

\begin{lstlisting}[language=ML, caption=Casing on runKern2,
    label=runKern,mathescape]
  fun runKern2 (Int, Int, $\ldots$) (Int, $\ldots$) (Int, $\ldots$) = $\ldots$
    | runKern2 (Int, Real, $\ldots$) (Int, $\ldots$) (Real, $\ldots$) = $\ldots$
    | runKern2 (Real, Int, $\ldots$) (Real, $\ldots$) (Int, $\ldots$) = $\ldots$
    | runKern2 (Real, Real, $\ldots$) (Real, $\ldots$) (Real, $\ldots$) = $\ldots$
    | runKern2 _ _ _ = raise Fail "Types don't match!";
\end{lstlisting}

However, as the number of types increase by $n$, the number of cases
we have to match on increases by $n^2$, and if we want to add
additional run functions like \texttt{runKern3}, the number of cases
would also increase polynomially. Furthermore, this only guarantees
type safety at runtime; we would like to statically guarantee, at
compile-time, that our kernels and executions thereof are
type-safe. In order to achieve that, we can introduce so-called
\emph{phantom types}, dummy types that are added as type variables
when declaring a type, but not necesarily used on the right hand side
of the declaration. We can use these extra type variables to enforce
type constraints in our functions. Consider this modified version of
Listing \ref{smlcl1}, which also includes a structure,
\texttt{OpenCL}, implementing the signature \texttt{OPENCL}, (for
simplicity, \texttt{bufP} and \texttt{kern2} are just implemented as
the unit type):

\begin{lstlisting}[language=ML, caption=Adding type
    variables,label=smlcl2,mathescape]
signature OPENCL = sig
  type $\alpha$ T;
  val Real : real T;
  val Int : int T;
  type $\alpha$ bufP;
  type ($\alpha$, $\beta$)kern2;

  (* Creates a buffer from a type constructor and a size *)
  val mkBuf : $\alpha$ T -> int -> $\alpha$ bufP

  (* Given the source code as a string, create a kernel that takes
     two buffers each specificed by a type T as arguments *)
  val mkKern2 : string -> ($\alpha$ T * $\beta$ T) -> ($\alpha$, $\beta$)kern2

  val runKern2 : ($\alpha$, $\beta$)kern2 -> ($\alpha$ bufP * $\beta$ bufP) -> unit
end;

structure OpenCL :> OPENCL = struct
  datatype $\alpha$ T = Real | Int;

  type $\alpha$ bufP = unit;
  type ($\alpha$, $\beta$)kern2 = unit;

  fun mkBuf t n = ();

  fun mkKern2 src (t1, t2) = ();

  fun runKern2 k (t1, t2) = ();
end;
\end{lstlisting}

Now, using the constructors \texttt{Real} and \texttt{Int}, we can
statically check that the types match up. For example \texttt{mkBuf
  Real 3} will now create a \texttt{real bufP}, which cannot be used
with a \texttt{(int, int)kern2}. Now \texttt{runKern2} can simply look
like this:

\begin{lstlisting}[language=ML, caption=runKern2 with type variables,
    label=runKern,mathescape]
  fun runKern2 (_, _, $\ldots$) (_, $\ldots$) (_, $\ldots$) = $\ldots$
\end{lstlisting}

While we could assign variables to the type constructors from the
second and third argument and dynamically check that they match with
\texttt{t1} and \texttt{t2}, our type signatues guarantee that they
always will.

Note that we need an opaque structure, or encapsulated data structures,
for this to work, otherwise we could construct statements such as
\texttt{runKern2 (mkKern2 "" (Real, Real)) (mkBuf Int 42, mkBuf Int
  43);}.

This technique also allows us to simulate a kind of ad-hoc
polymorphism, allowing us to perform casing on the types of the
arguments to a function. Suppose that we wanted to create a function
that reads the contents of a buffer into an array:

\begin{lstlisting}[language=ML, caption=readBuf signature, mathescape]
  val readBuf : $\alpha$ bufP -> $\alpha$ array
\end{lstlisting}

The function would have to first create an array with
\texttt{Array.array}, and then populate it with the contents of the
buffer. However, since the type information for the buffer is only
known at compile time, there is no way for us to correctly determine
the type of the array to make, or what initial elements should be put
in it. We could however include the type constructor in \texttt{bufP}
and perform the casing on that, leading us to this naive
implementation:

\begin{lstlisting}[language=ML, caption=Initial implementation of readBuf,
    mathescape]
  type $\alpha$ bufP = ($\alpha$ T * int * $\ldots$)

  fun readBuf (Real, n, $\ldots$) =
      let arr = Array.array(n, 0.0)
      in
          $\ldots$
      end;
    | readBuf (Int, n, $\ldots$) =
      let arr = Array.array(n, 0)
      in
          $\ldots$
      end;
\end{lstlisting}

However, this does not type check, as the first call to Array.array
unifies to a \texttt{real array}, while the other call unifies to an
\texttt{int array}. We're not completely at loss though; consider the
following implementation, where we've also included a new version of
the \texttt{$\alpha$T} datatype:

\begin{lstlisting}[language=ML, caption=readBuf implementation using
    closures,mathescape]
  type $\alpha$ bufP = ($\alpha$ T * int * $\ldots$)
  datatype $\alpha$ T = Real_ of int -> $\alpha$ array
               | Int_ of int -> $\alpha$ array;

  val Real : real T = Real_ (fn n => Array.array(n, 0.0));
  val Int : int T = Int_ (fn n => Array.array(n, 0));

  fun readBuf (Real_ f, n, $\ldots$) =
      let arr = f n
      in
          $\ldots$
      end;
    | readBuf (Int_ f, n, $\ldots$) =
      let arr = f n
      in
          $\ldots$
      end;
\end{lstlisting}

Here, the type constructors \texttt{Real\_} and \texttt{Int\_} include a
polymorphic function that we use to create the array with. When we
instantiate the type constructors using \texttt{Real} and
\texttt{Int}, we include a closure for that specific type, and since
it unifies with the phantom type provided, it all type checks. Note
that we could also have included a ``default'' value, such as $0.0$
and $0$, but we have chosen to use the first approach, since it adds a
lot of flexibility.
